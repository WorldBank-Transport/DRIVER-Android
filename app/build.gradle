buildscript {
    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.google.code.gson:gson:2.4'
        classpath 'org.joda:joda-convert:1.8.1'
        classpath 'javax.annotation:javax.annotation-api:1.2'
        classpath 'com.google.code.findbugs:jsr305:3.0.1'
        classpath 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
        classpath 'joda-time:joda-time:2.9.1'
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'ant', module: 'ant'
    all*.exclude group: 'awtui', module: 'awtui'
    all*.exclude group: 'swingui', module: 'swingui'
}

apply plugin: 'com.android.application'

android {

    if (!travisBuild) {
        signingConfigs {
            release
        }
    }
    
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    // Must specify individually all duplicated files from libraries to exclude or pickFirst
    packagingOptions {
        exclude 'NOTICE'
        exclude 'NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
    }
    defaultConfig {
        applicationId "org.worldbank.transport.driver"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-test.pro'

            if (!travisBuild) {
                signingConfig signingConfigs.release
            }
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-test.pro'

            if (!travisBuild) {
                signingConfig signingConfigs.release
            }
        }
    }
    dexOptions {
        incremental true
        javaMaxHeapSize "4096M"
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile 'com.google.code.gson:gson:2.4'
    compile('com.github.WorldBank-Transport:jsonschema2pojo:jsonschema2pojo-0.4.19-images') {
        // only want core
        exclude module: 'jsonschema2pojo-ant'
        exclude module: 'jsonschema2pojo-cli'
        exclude module: 'jsonschema2pojo-maven-plugin'
        exclude module: 'jsonschema2pojo-gradle-plugin'
        exclude module: 'jsonschema2pojo-integration-tests'

        exclude group: 'javax.validation', module: 'validation-api'
        exclude group: 'junit', module: 'junit'
        exclude group: 'android', module: 'net'
    }
    // used by jsonschema2pojo/hibernate validator
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'commons-lang:commons-lang:2.6'
    compile 'joda-time:joda-time:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'org.joda:joda-convert:1.8.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    // form builder
    compile 'com.github.azavea:AndroidValidatedForms:1.1.11'
    // Android libraries
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:multidex:1.0.1'
    // for device location API access
    compile 'com.google.android.gms:play-services-location:9.0.2'
    compile 'com.google.android.gms:play-services-auth:9.0.2'

    // secure class loading for downloaded model files
    compile 'it.necst.grabnrun:grabnrun:1.0.4'
    // example model files in library only used for test

    //androidTestCompile files('src/main/assets/models.jar')

    // test dependencies
    testCompile 'junit:junit:4.12'
    // mockito for unit tests
    testCompile 'org.mockito:mockito-core:1.10.19'
    // mockito for Android instrumentation tests
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    // mock web server and friends
    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-testing-support:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-ws:2.7.0'
    androidTestCompile 'org.bouncycastle:bcprov-jdk15on:1.53'
    androidTestCompile 'com.squareup.okio:okio:1.6.0'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'com.squareup.okhttp:okhttp-android-support:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-urlconnection:2.7.0'
    androidTestCompile 'org.codehaus.mojo:animal-sniffer-annotations:1.14'
}

def props = new Properties()

if (!travisBuild) {
    props.load(new FileInputStream(project.file("release.properties")))
    android.signingConfigs.release.storeFile project.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
}
